--Loja Virtual

datatype produto = Smartphone | TV
produtos = {(Smartphone, 450, 3), (TV, 870, 2)}

--O primeiro parâmetro de todos os canais é a identificação do acesso: 1 ou 2.
channel autenticaUsuario:{1..2}.{0}.{0}
channel adicionaCarrinho:{1..2}.produtos
channel acessaProduto:{1..2}.produtos
channel loginFalhou:{1..2}.{0..2}
channel acessoCliente:{1..2}
channel pesquisaProduto:{1..2}
channel confereItens:{1..2}
channel finalizaCompra:{1..2}
channel continuarComprando:{1..2}
channel confirmaEndereco:{1.2}
channel escolherFormaPagamento:{1..2}
channel confirmaPagamento:{1..2}
channel calcularFrete:{1..2}
channel concluirCompra:{1..2}.{450, 870, 900, 1320, 1740}
channel loginOk:{1..2} 
channel cadastrarNovoUsuario:{1..2}
channel exibeInfoProduto:{1..2}.{450,870}
channel removerItem:produtos
channel carrinhoVazio:{1..2}
channel pagamentoBoleto
channel pagamentoDebitoOnline
channel pagamentoCartaoCredito

acessosSimultaneos = {1..2}

-- Entrelaçamento de acessos
LOJAVIRTUAL = ||| i:acessosSimultaneos @ ACESSOCLIENTE(i)

--i=id acesso
ACESSOCLIENTE(i) = acessoCliente.i -> NAVEGACAO(i, <>)

--i=id acesso; c=carrinho
NAVEGACAO(i, c) = pesquisaProduto.i -> acessaProduto.i?item -> DETALHEPRODUTO(i, item, c)

-- Let .. Within
DETALHEPRODUTO(i, item, c) = let 
								preco = fnPreco(item) 
							 within
							 	exibeInfoProduto.i.preco -> adicionaCarrinho.i!item -> CARRINHO(i, <item>^c)

-- Comprehension
fnPreco(item) = head( seq({preco | (nome, preco, estoque) <- {item} }) )

--i=id acesso; c=carrinho
CARRINHO(i, c) = 	if length(c) == 0 then
						carrinhoVazio.i -> NAVEGACAO(i,c)
					---limite de 2 itens por carrinho (para evitar recursão infinita)
					else if length(c) < 2 and length(c)>0 then 
		  	  			(continuarComprando.i -> NAVEGACAO(i, c)
		  	  				[]
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAO(i, c, 0)
		  	  				[]
		  	  			-- Restrição de conjunto, só deve ser possível remover o que foi inserido.
		  	  			removerItem?iOut:set(c) -> CARRINHO(i, fnRemoveItem(iOut, c, <>)))
		  	  		-- Carrinho atingiu limite de itens (não pode continuar comprando)
		  	  		else 
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAO(i, c, 0)
		  	  				[]
		  	  			-- Restrição de conjunto, só deve ser possível remover o que foi inserido.
		  	  			removerItem?iOut:set(c) -> CARRINHO(i, fnRemoveItem(iOut, c, <>))

--iOut=item a remover; c=carrinho; checked=lista de itens ja comparados
fnRemoveItem(iOut, c, checked) = 	let 
										item = head(c)
						 			within 
						 				if (item == iOut) then
						 					tail(c) ^ checked
						 				else
					 						fnRemoveItem(iOut, tail(c), checked ^ <item>)

fnTotalCarrinho(c, t) = if length(c) > 0 then
							fnTotalCarrinho(tail(c), t + fnPreco( head(c)) )
						 else 
						 	t
-- i=id acesso; c=carrinho; n=tentativas de login.
AUTENTICACAO(i, c, n) = CADASTRONOVOUSUARIO(i,c,n)
					[]
		     	  autenticaUsuario.i?login.senha -> 
					((loginOk.i -> PAGAMENTO(i,c))
					  |~|
					(loginFalhou.i!n+1 -> 
							if(n+1 < 2) then 
								AUTENTICACAO(i, c, n+1) 
							else
								ACESSOCLIENTE(i)))

PAGAMENTO(i,c) = escolherFormaPagamento.i -> 
					(
						pagamentoBoleto -> ACESSOCLIENTE(i)
						[]
						pagamentoDebitoOnline -> ACESSOCLIENTE(i)
						[]
						pagamentoCartaoCredito -> ACESSOCLIENTE(i)
					)
					
CADASTRONOVOUSUARIO(i,c,n) = cadastrarNovoUsuario.i -> AUTENTICACAO(i,c,n)

assert LOJAVIRTUAL :[deadlock free[F]] -- PASSED
assert LOJAVIRTUAL :[deterministic [F]] -- FAIL
assert LOJAVIRTUAL :[divergence free [FD]] -- PASSED


--TODO versão normal: 
--acrescentar valor. 
--controle de estoque (concorrencia por recursos)
--acrescentar paralelismo sincronizado com cliente e/ou operadora de cartão de crédito.
--frete?

--
--TODO versão refinada: 
--retirar não determinismo do login

-- BANCO = transacaoBoleto -> transfereSaldo -> confirmaTransacao
-- 		[]
-- 		transacaoDebitoOnline -> verificaSaldo -> debitaConta -> creditaConta -> confirmaTransacao

-- OPERADORACARTAO = transacaoCartao -> checaDadosUsuario -> checaCreditoCliente -> autorizaPagamento