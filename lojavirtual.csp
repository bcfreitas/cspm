--Loja Virtual

datatype produto = Smartphone | TV
datatype pessoas = Joao | Maria

acessosSimultaneos = {1..2}
produtos = {Smartphone, TV}
produtosBD = {(Smartphone, 100, 2), (TV, 200, 1)}
usuarios = {(Joao, 123), (Maria, 345)}
nomesPossiveis = {Joao, Maria}

--os conjuntos abaixo foram criados para melhorar o desempenho do FDR
totaisPossiveis = {100,200,300,400}
precosUnitariosPossiveis = {100,200}

--O primeiro parametro de todos os canais é a identificacao do acesso: 1 ou 2, para nao nos perdermos no Probe! :)
channel autenticaUsuario:{1..2}.{Joao, Maria}.{123, 345}
channel retornoLogin:{1..2}.Bool
channel adicionaCarrinho:{1..2}.produtos
channel acessaProduto:{1..2}.produtos
channel acessoCliente:{1..2}
channel pesquisaProduto:{1..2}
channel continuarComprando:{1..2}
channel escolherFormaPagamento:{1..2}.totaisPossiveis
channel concluirCompra:{1..2}.totaisPossiveis
channel removerItem:{1..2}.produtos
channel carrinhoVazio:{1..2}
channel pagamentoDebitoOnline:{1..2}.totaisPossiveis
channel confirmaTransacaoCredito:{1..2}.Bool 
channel confirmaTransacaoDebito:{1..2}.Bool
channel enviaProdutos:{1..2}
channel handshakeBanco:{1..2}.totaisPossiveis
channel aguardaResposta:{1..2}
channel transacaoCredito:{1..2}
channel handshakeOperadora:{1..2}.totaisPossiveis.{Joao, Maria}.{1111111,2222222}.{11,22}
channel transacaoDebitoOnline:{1..2}.totaisPossiveis.{333333,444444}.nomesPossiveis.{321,654}
channel pagamentoCartaoCredito:{1..2}.totaisPossiveis
channel informaDadosCartao:{1..2}.{Joao, Maria}.{1111111,2222222}.{11,22}
channel eventoInternoOperadora:{1..2}
channel eventoInternoBanco:{1..2}
channel separaEstoque:{1..2}.produtos
channel reverteEstoque:{1..2}.produtos
channel mensagemEstoqueIndisponivel:{1..2}.produtos
channel respostaEstoqueBD:{1..2}.Bool.precosUnitariosPossiveis
channel consultaProdutoBD:{1..2}.produtos
channel respostaProdutoBD:{1..2}.produtos.precosUnitariosPossiveis.{0..9}
channel depositoCliente:{333333, 444444}.{100}

--Processo principal
--Entrelacamento de acessos + composicoes generalizadas
LOJAVIRTUAL = 	( ( ( (||| i:acessosSimultaneos @ ACESSOCLIENTE(i))
												[| {|separaEstoque, respostaEstoqueBD, consultaProdutoBD, respostaProdutoBD, reverteEstoque,
																autenticaUsuario, retornoLogin|} |]
																	BDLOJA(seq(produtosBD)) )
												[| {|handshakeBanco, confirmaTransacaoDebito|} |] 
																	--hiding de depositoCliente para causar divergência
																	BANCO \{|depositoCliente|})
												[| {|handshakeOperadora, transacaoCredito, confirmaTransacaoCredito|} |] 
																	OPERADORACARTAO ) 

--Servidor de banco de dados da loja.
BDLOJA(produtosLoja) = 	--Todos os eventos iniciais aqui sao sincronizados com outros processos (ver processo principal)
						--Usado no momento que o cliente clica no produto para detalhar suas informacoes
						(consultaProdutoBD?i?item -> CONSULTABD(i, produtosLoja, item))
							[]
						--Usado ao adicionar no carrinho, abate o estoque
						(separaEstoque?i?item -> SEPARAESTOQUEBD(i, produtosLoja, item, <>))
							[]
						--Usado quando o usuario retira um item do carrinho
						(reverteEstoque?i?item -> REVERTEESTOQUEBD(i, produtosLoja, item, <>))
							[]
						--Na versao basica a resposta do login é nao-deterministica
						(autenticaUsuario?i?login?senha -> 
											((retornoLogin!i.true -> BDLOJA(produtosLoja))
												|~|
											(retornoLogin!i.false -> BDLOJA(produtosLoja))))

CONSULTABD(i, produtosLoja, item) = let 
										preco = fnPreco(produtosLoja, item)
	 									estoque = fnEstoque(produtosLoja, item) 
	 								within
	 									respostaProdutoBD!i.item.preco.estoque -> BDLOJA(produtosLoja)

--checked é um array usado para controle na recursao
SEPARAESTOQUEBD(i, produtosLoja, item, checked) = 	let
														nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														preco = head( seq({preco | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														estoque = head( seq({estoque | (nome, preco, estoque) <- {head(produtosLoja)} }) )
													within
														if nome == item then
															if estoque > 0 then
																--Abate 1 no estoque
																respostaEstoqueBD!i.true.preco ->
																	BDLOJA(<(nome,preco,estoque-1)>^(tail(produtosLoja))^checked)
															else
																respostaEstoqueBD!i.false.preco -> BDLOJA(produtosLoja^checked)
														else
															SEPARAESTOQUEBD(i, tail(produtosLoja), item, checked^<head(produtosLoja)>)

REVERTEESTOQUEBD(i, produtosLoja, item, checked) = 	let
														nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														preco = head( seq({preco | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														estoque = head( seq({estoque | (nome, preco, estoque) <- {head(produtosLoja)} }) )
													within
														if nome == item then
															-- 9 é o limite maximo de estoque de itens, definido no canal respostaProdutoBD
															if estoque < 9 then
																-- Adiciona 1 (reverte) no estoque
																BDLOJA(<(nome,preco,estoque+1)>^(tail(produtosLoja))^checked)
															else
																BDLOJA(<(nome,preco,estoque)>^(tail(produtosLoja))^checked)
														else
															REVERTEESTOQUEBD(i, tail(produtosLoja), item, checked^<head(produtosLoja)>)

--i=id acesso
ACESSOCLIENTE(i) = acessoCliente.i -> NAVEGACAO(i, <>)

--i=id acesso; c=carrinho
NAVEGACAO(i, c) = pesquisaProduto.i -> acessaProduto.i?item ->  DETALHEPRODUTO(i, item, c)

-- ao visualizar o detalhe de um produto, o cliente pode adicionar ao carrinho, fazendo com que seja separado estoque no banco.
DETALHEPRODUTO(i, item, c) =	consultaProdutoBD!i!item -> respostaProdutoBD!i?item?preco?estoque ->
														(if estoque > 0 then
															(adicionaCarrinho.i.item -> 
																(
																--separaEstoque e RespostaEstoqueBD sao sincronizados com BDLOJA
																separaEstoque!i!item -> 
																	respostaEstoqueBD!i?retorno?preco ->
																				if retorno == true then
																					--ao adicionar item no carrinho a quantidade padrao é 1
																					CARRINHO(i, <(item,preco,1)>^c)
																				else
																					mensagemEstoqueIndisponivel.i.item -> 
																								NAVEGACAO(i, c) 
																)
															)
															else
																mensagemEstoqueIndisponivel.i.item -> 
																								NAVEGACAO(i, c)
														)
															[]
														NAVEGACAO(i,c)

-- Comprehension foi bastante usado para separar dados em tuplas
fnPreco(produtosLoja, item) = 	let 
									nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) ) 
									preco = head( seq({preco | (nome, preco, estoque) <- {head(produtosLoja)} }) ) 
								within
									if nome == item then
										preco
									else
										fnPreco(tail(produtosLoja), item)

fnPreco2(item) = head( seq({preco | (nome, preco, estoque) <- {item} }) ) 

fnEstoque(produtosLoja, item) = let 
									nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) ) 
									estoque = head( seq({estoque | (nome, preco, estoque) <- {head(produtosLoja)} }) ) 
								within
									if nome == item then
										estoque
									else
										fnEstoque(tail(produtosLoja), item)

--i=id acesso; c=carrinho
CARRINHO(i, c) = 	if length(c) == 0 then
						carrinhoVazio.i -> NAVEGACAO(i,c)
					---limite de 2 itens por carrinho (para evitar recursao infinita)
					else if length(c) < 2 and length(c)>0 then 
		  	  			(continuarComprando.i -> NAVEGACAO(i, c)
		  	  				[]
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAO(i, c, 0)
		  	  				[]
		  	  			-- Restricao de conjunto (:fnIdItens(c,<>), so deve ser possivel remover o que foi inserido.
		  	  			-- reverteEstoque é sincronizado com BDLOJA
		  	  			removerItem!i?iOut:fnIdItens(c,<>) -> reverteEstoque!i.iOut -> CARRINHO(i, fnRemoveItem(iOut, c, <>)))
		  	  		-- Carrinho atingiu limite de itens (nao pode continuar comprando)
		  	  		else 
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAO(i, c, 0)
		  	  				[]
		  	  			-- Restricao de conjunto, so deve ser possivel remover o que foi inserido.
		  	  			-- reverteEstoque é sincronizado com BDLOJA
		  	  			removerItem!i?iOut:fnIdItens(c,<>) -> reverteEstoque!i.iOut -> CARRINHO(i, fnRemoveItem(iOut, c, <>))

--iOut=item a remover; c=carrinho; checked=lista de itens ja comparados
fnRemoveItem(iOut, c, checked) = 	let 
										item = head(c)
										nome = head( seq({nome | (nome, preco, estoque) <- {item} }) )
						 			within 
						 				if (nome == iOut) then
						 					tail(c) ^ checked
						 				else
					 						fnRemoveItem(iOut, tail(c), checked^<item>)

fnTotalCarrinho(c, t) = if length(c) > 0 then
							fnTotalCarrinho(tail(c), t + fnPreco2( head(c)) )
						 else 
						 	t

fnIdItens(c,idItens) = 	let 
							item = head(c)
							nome = head( seq({nome | (nome, preco, estoque) <- {item} }) ) 
						within
							if length(c) > 0 then
								fnIdItens(tail(c), idItens^<nome>)
							else
								set(idItens)

-- i=id acesso; c=carrinho; n=tentativas de login.
AUTENTICACAO(i, c, n) = autenticaUsuario!i?login.senha ->
														retornoLogin.i?resposta ->
															if resposta == true then
																PAGAMENTO(i,c)
															else 
																if(n+1 < 2) then 
																	AUTENTICACAO(i, c, n+1) 
																else
																	ACESSOCLIENTE(i)

PAGAMENTO(i,c) = 	let 
						total = fnTotalCarrinho(c,0) 
					within
						escolherFormaPagamento.i.total -> 
									((pagamentoDebitoOnline!i.total -> PAGAMENTODEBITOONLINE(i,c,total))
										[]
									(pagamentoCartaoCredito!i.total -> PAGAMENTOCREDITO(i, c, total)))	

--eventos sincronizados com BANCO (ver composicao paralela)
PAGAMENTODEBITOONLINE(i,c,total) = handshakeBanco!i.total -> aguardaResposta!i ->
																		confirmaTransacaoDebito!i?resposta -> 
																			if resposta == true then
																				enviaProdutos.i -> ACESSOCLIENTE(i)
																			else
																				PAGAMENTO(i,c)

--eventos sincronizados com OPERADORACARTAO (ver composicao paralela)																
PAGAMENTOCREDITO(i, c, total) = informaDadosCartao!i?nomegrafado?cartao?codigo -> 
									handshakeOperadora.i.total.nomegrafado.cartao.codigo -> 
																			aguardaResposta.i ->
																					confirmaTransacaoCredito!i?resposta -> 
																						if resposta == true then
																							enviaProdutos!i -> ACESSOCLIENTE(i)
																						else 
																							PAGAMENTO(i,c)

BANCO = (handshakeBanco?i?total ->
					transacaoDebitoOnline!i.total?conta?usuario?senha -> 
														eventoInternoBanco!i -> 
															(
																--Na versao basica a autorizacao do DébitoOnline é nao-deterministica.
																(confirmaTransacaoDebito!i!true -> BANCO)
																	|~|
																(confirmaTransacaoDebito!i!false -> BANCO)
															) )
			|~|
		--Aqui a divergência (esta sob hiding na composicao): o cliente pode depositar aleatoriamente.
		depositoCliente?conta?quantia -> BANCO

OPERADORACARTAO = handshakeOperadora?i?total?nomegrafado?cartao?codigo -> eventoInternoOperadora!i ->
															(
																--Na versao basica a autorizacao da Operadora é nao-deterministica.
																(confirmaTransacaoCredito!i!true -> OPERADORACARTAO)
																	|~|
																(confirmaTransacaoCredito!i!false -> OPERADORACARTAO)
															)
			


assert LOJAVIRTUAL :[deadlock free[F]] -- PASSED
assert LOJAVIRTUAL :[deterministic [F]] -- FAIL
assert LOJAVIRTUAL :[divergence free [FD]] -- FAIL

---------------------------------------------------------------------------------------------------------------------------------------------
---------------------------------------------------------------------------------------------------------------------------------------------
---------------------------------------------------------------------------------------------------------------------------------------------
---------------------------------------------------------------------------------------------------------------------------------------------
---------------------------------------------------------------------------------------------------------------------------------------------
--RRRRRRRRRRRRRRRRR                          ffffffffffffffff    iiii                                                d::::::d                  
--R::::::::::::::::R                        f::::::::::::::::f  i::::i                                               d::::::d                  
--R::::::RRRRRR:::::R                      f::::::::::::::::::f  iiii                                                d::::::d                  
--RR:::::R     R:::::R                     f::::::fffffff:::::f                                                      d:::::d                   
--  R::::R     R:::::R    eeeeeeeeeeee     f:::::f       ffffffiiiiiiinnnn  nnnnnnnn      aaaaaaaaaaaaa      ddddddddd:::::d   aaaaaaaaaaaaa   
--  R::::R     R:::::R  ee::::::::::::ee   f:::::f             i:::::in:::nn::::::::nn    a::::::::::::a   dd::::::::::::::d   a::::::::::::a  
--  R::::RRRRRR:::::R  e::::::eeeee:::::eef:::::::ffffff        i::::in::::::::::::::nn   aaaaaaaaa:::::a d::::::::::::::::d   aaaaaaaaa:::::a 
--  R:::::::::::::RR  e::::::e     e:::::ef::::::::::::f        i::::inn:::::::::::::::n           a::::ad:::::::ddddd:::::d            a::::a 
--  R::::RRRRRR:::::R e:::::::eeeee::::::ef::::::::::::f        i::::i  n:::::nnnn:::::n    aaaaaaa:::::ad::::::d    d:::::d     aaaaaaa:::::a 
--  R::::R     R:::::Re:::::::::::::::::e f:::::::ffffff        i::::i  n::::n    n::::n  aa::::::::::::ad:::::d     d:::::d   aa::::::::::::a 
--  R::::R     R:::::Re::::::eeeeeeeeeee   f:::::f              i::::i  n::::n    n::::n a::::aaaa::::::ad:::::d     d:::::d  a::::aaaa::::::a 
--  R::::R     R:::::Re:::::::e            f:::::f              i::::i  n::::n    n::::na::::a    a:::::ad:::::d     d:::::d a::::a    a:::::a 
--RR:::::R     R:::::Re::::::::e          f:::::::f            i::::::i n::::n    n::::na::::a    a:::::ad::::::ddddd::::::dda::::a    a:::::a 
--R::::::R     R:::::R e::::::::eeeeeeee  f:::::::f            i::::::i n::::n    n::::na:::::aaaa::::::a d:::::::::::::::::da:::::aaaa::::::a 
--R::::::R     R:::::R  ee:::::::::::::e  f:::::::f            i::::::i n::::n    n::::n a::::::::::aa:::a d:::::::::ddd::::d a::::::::::aa:::a
--RRRRRRRR     RRRRRRR    eeeeeeeeeeeeee  fffffffff            iiiiiiii nnnnnn    nnnnnn  aaaaaaaaaa  aaaa  ddddddddd   ddddd  aaaaaaaaaa  aaaa

--(cartao, cdc, nome, limite de credito)
repositorioCartoes={(1111111, 11, Joao, 500), (2222222, 22, Maria, 100)}
--(conta, usuario, senha, saldo em conta)
repositorioContas={(333333, Joao, 321, 100), (444444, Maria, 654, 300)}
--usado como dados iniciais quando ocorrer deposito de cliente (para evitar confusao porque o nome do conjunto foi usado como nome de parametro)
repositorioContasRefill=repositorioContas

--Processo principal
--Entrelacamento de acessos + composicoes generalizadas
LOJAVIRTUALref =  ( ( ( (||| i:acessosSimultaneos @ ACESSOCLIENTEref(i))
												[| {|separaEstoque, respostaEstoqueBD, consultaProdutoBD, respostaProdutoBD, reverteEstoque,
															autenticaUsuario, retornoLogin|} |]
																	BDLOJAref(seq(produtosBD), seq(usuarios)) )
												[| {|handshakeBanco, confirmaTransacaoDebito|} |] 
																	BANCOref(seq(repositorioContas) ) \ {|depositoCliente|} )
												[| {|handshakeOperadora, transacaoCredito, confirmaTransacaoCredito|} |] 
																	OPERADORACARTAOref(seq(repositorioCartoes)) ) 


BDLOJAref(produtosLoja, usuarios) = 	
							(consultaProdutoBD?i?item -> CONSULTABDref(i, produtosLoja, item, usuarios))
								[]
							(separaEstoque?i?item -> SEPARAESTOQUEBDref(i, produtosLoja, item, <>, usuarios))
								[]
							(reverteEstoque?i?item -> REVERTEESTOQUEBDref(i, produtosLoja, item, <>, usuarios))
								[]
							(autenticaUsuario?i?nome?senha -> AUTENTICAUSUARIOBDref(i, nome, senha, usuarios, <>, produtosLoja))


CONSULTABDref(i, produtosLoja, item, usuarios) = 	
									let 
										preco = fnPreco(produtosLoja, item)
	 									estoque = fnEstoque(produtosLoja, item) 
	 								within
	 									respostaProdutoBD!i.item.preco.estoque -> BDLOJAref(produtosLoja, usuarios)

SEPARAESTOQUEBDref(i, produtosLoja, item, checked, usuarios) = 	
													let
														nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														preco = head( seq({preco | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														estoque = head( seq({estoque | (nome, preco, estoque) <- {head(produtosLoja)} }) )
													within
														if nome == item then
															if estoque > 0 then
																respostaEstoqueBD!i.true.preco -> 
																	BDLOJAref(<(nome,preco,estoque-1)>^(tail(produtosLoja))^checked, usuarios)
															else
																respostaEstoqueBD!i.false.preco -> BDLOJAref(produtosLoja^checked, usuarios)
														else
															SEPARAESTOQUEBDref(i, tail(produtosLoja), item, checked^<head(produtosLoja)>, usuarios)

REVERTEESTOQUEBDref(i, produtosLoja, item, checked, usuarios) = 	
													let
														nome = head( seq({nome | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														preco = head( seq({preco | (nome, preco, estoque) <- {head(produtosLoja)} }) )
														estoque = head( seq({estoque | (nome, preco, estoque) <- {head(produtosLoja)} }) )
													within
														if nome == item then
															if estoque < 9 then
																BDLOJAref(<(nome,preco,estoque+1)>^(tail(produtosLoja))^checked, usuarios)
															else
																BDLOJAref(<(nome,preco,estoque)>^(tail(produtosLoja))^checked, usuarios)
														else
															REVERTEESTOQUEBDref(i, tail(produtosLoja), item, checked^<head(produtosLoja)>, usuarios)

AUTENTICAUSUARIOBDref(i, nome, senha, usuarios, checked, produtosLoja) = 	
															--Aqui foi eliminado o nao-determinismo da versao basica
															let
																nomeBD = head( seq({nome | (nome, senha) <- {head(usuarios)} }) )
																senhaBD = head( seq({senha | (nome, senha) <- {head(usuarios)} }) )
															within
																if nome == nomeBD then
																	if senha == senhaBD then
																		retornoLogin.i!true-> BDLOJAref(produtosLoja, usuarios^checked)
																	else 
																		retornoLogin.i!false -> BDLOJAref(produtosLoja, usuarios^checked)
																else 
																	AUTENTICAUSUARIOBDref(i, nome, senha, tail(usuarios), <head(usuarios)>^checked, produtosLoja)

--i=id acesso
ACESSOCLIENTEref(i) = acessoCliente.i -> NAVEGACAOref(i, <>)

--i=id acesso; c=carrinho
NAVEGACAOref(i, c) = pesquisaProduto.i -> acessaProduto.i?item ->  DETALHEPRODUTOref(i, item, c)

-- ao visualizar o detalhe de um produto, o cliente pode adicionar ao carrinho, fazendo com que seja separado estoque no banco.
DETALHEPRODUTOref(i, item, c) =	consultaProdutoBD!i!item -> respostaProdutoBD!i?item?preco?estoque ->
														(if estoque > 0 then
															(adicionaCarrinho.i.item -> 
																(
																--separaEstoque e RespostaEstoqueBD sao sincronizados com BDLOJA
																separaEstoque!i!item -> 
																	respostaEstoqueBD!i?retorno?preco ->
																				if retorno == true then
																					--ao adicionar item no carrinho a quantidade padrao é 1
																					CARRINHOref(i, <(item,preco,1)>^c)
																				else
																					mensagemEstoqueIndisponivel.i.item -> 
																								NAVEGACAOref(i, c) 
																)
															)
															else
																mensagemEstoqueIndisponivel.i.item -> 
																								NAVEGACAOref(i, c)
														)
															[]
														NAVEGACAOref(i,c)

--i=id acesso; c=carrinho
CARRINHOref(i, c) = 	if length(c) == 0 then
						carrinhoVazio.i -> NAVEGACAOref(i,c)
					---limite de 2 itens por carrinho (para evitar recursao infinita)
					else if length(c) < 2 and length(c)>0 then 
		  	  			(continuarComprando.i -> NAVEGACAOref(i, c)
		  	  				[]
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAOref(i, c, 0)
		  	  				[]
		  	  			-- Restricao de conjunto, so deve ser possivel remover o que foi inserido.
		  	  			-- reverteEstoque é sincronizado com BDLOJA
		  	  			removerItem!i?iOut:fnIdItens(c,<>) -> reverteEstoque!i.iOut -> CARRINHOref(i, fnRemoveItem(iOut, c, <>)))
		  	  		-- Carrinho atingiu limite de itens (nao pode continuar comprando)
		  	  		else 
		  	  			concluirCompra.i.fnTotalCarrinho(c,0) -> AUTENTICACAOref(i, c, 0)
		  	  				[]
		  	  			-- Restricao de conjunto, so deve ser possivel remover o que foi inserido.
		  	  			-- reverteEstoque é sincronizado com BDLOJA
		  	  			removerItem!i?iOut:fnIdItens(c,<>) -> reverteEstoque!i.iOut -> CARRINHOref(i, fnRemoveItem(iOut, c, <>))

-- i=id acesso; c=carrinho; n=tentativas de login.
AUTENTICACAOref(i, c, n) = autenticaUsuario.i?login.senha -> 
													(
														retornoLogin.i?resposta -> 
															if resposta == true then
																PAGAMENTOref(i,c) 
															else
											  				 	if(n+1 < 2) then 
																	AUTENTICACAOref(i, c, n+1) 
																else
																	ACESSOCLIENTEref(i)
													)

PAGAMENTOref(i,c) = 	let 
									total = fnTotalCarrinho(c,0) 
								within
									escolherFormaPagamento.i.total -> 
												((pagamentoDebitoOnline!i.total -> PAGAMENTODEBITOONLINEref(i,c,total))
													[]
												(pagamentoCartaoCredito!i.total -> PAGAMENTOCREDITOref(i, c, total)))	

--eventos sincronizados com BANCO (ver composicao paralela)
PAGAMENTODEBITOONLINEref(i,c,total) = handshakeBanco!i.total -> 
																		aguardaResposta!i ->
																			confirmaTransacaoDebito!i?resposta -> 
																				if resposta == true then
																					enviaProdutos.i -> ACESSOCLIENTEref(i)
																				else
																					PAGAMENTOref(i,c)

--eventos sincronizados com OPERADORACARTAO (ver composicao paralela)																
PAGAMENTOCREDITOref(i, c, total) = informaDadosCartao!i?nome?cartao?codigo -> 
									handshakeOperadora.i.total.nome.cartao.codigo -> aguardaResposta.i ->
															confirmaTransacaoCredito!i?resposta ->
																if resposta == true then
																	enviaProdutos!i -> ACESSOCLIENTEref(i)
																else
																	 PAGAMENTOref(i,c)

BANCOref(repositorioContas) = 
	--A divergencia foi eliminada com esta condicao: a opcao interna de depositoCliente so eh dada quando todas as contas zeram
	if fnContasZeradas(repositorioContas) then
		(handshakeBanco?i?total ->
						transacaoDebitoOnline!i.total?conta?usuario?senha ->
													eventoInternoBanco!i -> 
														AUTORIZACAOBANCOref(i, total, conta, usuario, senha, repositorioContas, <>) )
			|~|
		--Aqui esta a divergencia (depositoCliente esta sob hiding na composicao paralela com LOJAVIRTUAL)
		--Tambem foi usada uma simplificacao: qualquer conta/quantia das possiveis gera um refill para o status inicial das contas.
		depositoCliente?conta?quantia -> BANCOref(seq(repositorioContasRefill))
	else
		(handshakeBanco?i?total ->
				transacaoDebitoOnline!i.total?conta?usuario?senha ->
											eventoInternoBanco!i -> 
												AUTORIZACAOBANCOref(i, total, conta, usuario, senha, repositorioContas, <>) )

fnContasZeradas(repositorioContas) = 	
								if length(repositorioContas)>0 then
									let
										saldo = head(seq({saldo | (conta, usuario, senha, saldo) <- {head(repositorioContas)} }))	
									within
										if saldo > 0 then
											false
										else
											fnContasZeradas(tail(repositorioContas))
								else
									true


AUTORIZACAOBANCOref(i, total,conta,usuario,senha,repositorioContas, checked) =  
									--Aqui foi eliminado o nao-determinismo da versao basica
									let 
										contaBD = head(seq({conta | (conta, usuario, senha, saldo) <- {head(repositorioContas)} }))	
										usuarioBD = head(seq({usuario | (conta, usuario, senha, saldo) <- {head(repositorioContas)} }))	
										senhaBD = head(seq({senha | (conta, usuario, senha, saldo) <- {head(repositorioContas)} }))	
										saldo = head(seq({saldo | (conta, usuario, senha, saldo) <- {head(repositorioContas)} }))	
									within
										if 
											-- chave primaria da busca
											conta==contaBD 
										then
											if
												--os dados de login no sistema bancario estao ok.
												usuario==usuarioBD and senha==senhaBD 
											then
												if 
													-- ha saldo disponivel na conta
													saldo >= total
												then
													-- autoriza transacao
													confirmaTransacaoDebito!i!true -> BANCOref(<(conta, usuario, senha, saldo-total)>^tail(repositorioContas)^checked)
												else
													-- nao ha saldo, nao autoriza
													confirmaTransacaoDebito!i!false -> BANCOref(repositorioContas^checked)
											else
												-- os dados de login nao conferem, nega transacao
												confirmaTransacaoDebito!i!false -> BANCOref(repositorioContas^checked)
										else
											-- nao é o registro desejado, busca o proximo.
											AUTORIZACAOBANCOref(i,total,conta,usuario,senha, tail(repositorioContas), <head(repositorioContas)>^checked )


OPERADORACARTAOref(repositorioCartoes) = handshakeOperadora?i?total?nome?cartao?codigo -> 
													eventoInternoOperadora!i -> AUTORIZACAOOPERADORAref(i, total, cartao, codigo, nome, repositorioCartoes,<>) 

AUTORIZACAOOPERADORAref(i, total, cartao, codigo, nome, repositorioCartoes, checked) = 	
						--Aqui foi eliminado o nao-determinismo da versao basica
						let
							cartaoBD = head(seq({cartao | (cartao, codigo, nome, credito) <- {head(repositorioCartoes)} }))	
							codigoBD = head(seq({codigo | (cartao, codigo, nome, credito) <- {head(repositorioCartoes)} }))	
							nomeBD = head(seq({nome | (cartao, codigo, nome, credito) <- {head(repositorioCartoes)} }))
							credito = head(seq({credito | (cartao, codigo, nome, credito) <- {head(repositorioCartoes)} }))	
						within
							if 
								--a chave primaria na busca
								cartao==cartaoBD 
							then
								if
									--os parametros informados para o cartao estao corretos
									codigo==codigoBD and nome==nomeBD 
								then
									if 
										--ha limite de crédito disponivel para o cliente
										credito >= total 
									then
										-- autoriza e diminui limite do cliente
										confirmaTransacaoCredito!i!true -> 
											OPERADORACARTAOref(<(cartaoBD, codigoBD, nome, credito-total)>^tail(repositorioCartoes^checked))
									else
										-- nao ha limite disponivel: nao autoriza
										confirmaTransacaoCredito!i!false -> OPERADORACARTAOref(repositorioCartoes^checked)
								else
									-- os parametros informados do cartao sao invalidos, nega transacao.
									confirmaTransacaoCredito!i!false -> OPERADORACARTAOref(repositorioCartoes^checked)
							else
								-- nao é o registro desejado, passa para o proximo.
								AUTORIZACAOOPERADORAref(i, total, cartao, codigo, nome, tail(repositorioCartoes), <head(repositorioCartoes)>^checked)
								


assert LOJAVIRTUALref :[deadlock free[F]] -- PASSED
assert LOJAVIRTUALref :[deterministic [F]] -- PASSED
assert LOJAVIRTUALref :[divergence free [FD]] -- PASSED

assert LOJAVIRTUAL [T= LOJAVIRTUALref -- PASSED
assert LOJAVIRTUALref [T= LOJAVIRTUAL -- FAIL

assert LOJAVIRTUAL [F= LOJAVIRTUALref -- PASSED (sem a divergencia)
assert LOJAVIRTUALref [F= LOJAVIRTUAL -- FAIL

assert LOJAVIRTUAL [FD= LOJAVIRTUALref -- PASSED
assert LOJAVIRTUALref [FD= LOJAVIRTUAL -- FAIL